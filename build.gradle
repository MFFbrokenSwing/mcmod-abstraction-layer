buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "net.ltgt.apt" version "0.20"
    id "com.github.hierynomus.license" version "0.15.0"
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: "com.github.hierynomus.license"

    group 'com.github.mffbrokenswing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    license {
        header = file(rootDir.toString() + '/HEADER')
        mapping {
            java = "SLASHSTAR_STYLE"
        }
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'MFFBrokenSwing'
        strictCheck = true
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    classes.dependsOn licenseFormat

}

project(":abstraction") {
    archivesBaseName = "mcmod-abstraction-layer"
    version = '1.0.0-SNAPSHOT'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.2'
    }
}

project(":forge-bindings-1.12") {
    archivesBaseName = "forge-bindings-1.12"
    version = '1.0.0-SNAPSHOT'

    dependencies {
        api project(":abstraction")
    }
}

project(":example-mod") {
    archivesBaseName = "example-mod"
    version = '1.0.0-SNAPSHOT'
    
    dependencies {
        api project(":abstraction")
        annotationProcessor project(":forge-bindings-1.12")
    }
}
